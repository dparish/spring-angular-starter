plugins {
    id "com.moowork.node" version "1.2.0"
}

apply plugin: 'base'

ext {
    backendTarget = project(":backend").getProjectDir().getPath() + "/src/main/resources/public"
    ng = file("${projectDir.path}/node_modules/@angular/cli/bin/ng")
}

task serve(type: NodeTask) {
    script = ng
    args = ['serve']
}

task buildApp(type: NodeTask) {
    dependsOn npmInstall
    inputs.dir file("src")
    inputs.dir file("node_modules")
    inputs.dir file(".angular-cli.json")
    inputs.dir file("package.json")
    outputs.dir ("${backendTarget}")
    script = ng
    args = ["build", "--output-path=${backendTarget}"]
}

task watch(type: NodeTask) {
    dependsOn npmInstall
    script = ng
    args = ["build", "--watch", "--output-path=${backendTarget}"]
}

task lint(type: NodeTask) {
    dependsOn npmInstall
    script = ng
    args = ["lint", "--type-check"]
}

task test(type: NodeTask) {
    dependsOn npmInstall
    script = ng
    args = ["test", "--single-run"]
}

clean.doFirst {
    delete "dist"
    delete "tmp"
    delete "src/app/model/generated.d.ts"
}

check.dependsOn lint
check.dependsOn test
